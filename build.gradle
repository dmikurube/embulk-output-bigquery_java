plugins {
    id "com.jfrog.bintray" version "1.1"
    id "com.github.johnrengelman.shadow" version "4.0.4"
    id "java"
    id "checkstyle"
    id "maven-publish"
    id "org.embulk.embulk-plugins" version "0.3.0"
}
repositories {
    mavenCentral()
    jcenter()
}
configurations {
    provided
}

group = "io.trocco"
version = "0.0.8"
// version = "0.0.8-SNAPSHOT"
description = "Bigquery output for embulk"

// version = "0.0.7"

sourceCompatibility = 1.8
targetCompatibility = 1.8

// Relocate Guava packages since it's incompatible with Guava's version from Embulk
shadowJar {
    classifier = "shadow"
    exclude "org/embulk/plugin/**"

    dependencies {
        include dependency("com.google.guava:guava")
        include dependency("com.google.http-client:google-http-client")
    }

    relocate "com.google.common", "embulk.bigquery.com.google.common"
}


dependencies {
    compile "org.embulk:embulk-core:0.9.17"

    compile 'com.fasterxml.jackson.core:jackson-core:2.10.2'
    compile 'com.google.cloud:google-cloud-bigquery:1.106.0'

    // compile "YOUR_JAR_DEPENDENCY_GROUP:YOUR_JAR_DEPENDENCY_MODULE:YOUR_JAR_DEPENDENCY_VERSION"
    testCompile "junit:junit:4.+"
    testCompile "org.slf4j:slf4j-simple:1.7.12"
    testCompile 'org.embulk:embulk-standards:0.9.23'
    testCompile 'org.embulk:embulk-test:0.9.23'
    testCompile "org.mockito:mockito-core:1.+"
    testCompile "org.embulk:embulk-core:0.9.23:tests"
    testCompile "org.embulk:embulk-deps-buffer:0.9.23"
    testCompile "org.embulk:embulk-deps-config:0.9.23"
}

embulkPlugin {
    mainClass = "org.embulk.output.bigquery_java.BigqueryJavaOutputPlugin"
    category = "output"
    type = "bigquery_java"
}

publishing {
    publications {
        embulkPluginMaven(MavenPublication) {  // Publish it with "publishEmbulkPluginMavenPublicationToMavenRepository".
            from components.java  // Must be "components.java". The dependency modification works only for it.
        }
    }
    repositories {
        maven {
            url = "${project.buildDir}/mavenPublishLocal"
        }
    }
}

task classpath(type: Copy, dependsOn: ["jar", "shadowJar"]) {
    doFirst { file("classpath").deleteDir() }
    from (configurations.runtime - configurations.provided
            + configurations.shadow
            - files(shadowJar.getIncludedDependencies())
            + files(shadowJar.archivePath))
    into "classpath"
}
clean { delete "classpath" }

checkstyle {
    configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    toolVersion = '6.14.1'
}
checkstyleMain {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
checkstyleTest {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
task checkstyle(type: Checkstyle) {
    classpath = sourceSets.main.output + sourceSets.test.output
    source = sourceSets.main.allJava + sourceSets.test.allJava
}

gem {
    from("LICENSE")  // Optional -- if you need other files in the gem.
    authors = [ "giwa" ]
    email = [ "ugw.gi.world@gmail.com" ]
    // "description" of the gem is copied from "description" of your Gradle project.
    summary = "Bigquery output plugin for Embulk"
    homepage = "https://github.com/trocco-io/embulk-output-bigquery_java"
    licenses = [ "Apache-2.0" ]
}

gemPush {
    host = "https://rubygems.org"
}